// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'checkstyle'

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.5'
    testCompile 'junit:junit:4.11'
    testCompile 'info.cukes:cucumber-java:1.2.3'
    testCompile 'info.cukes:cucumber-junit:1.2.3'
    testCompile 'mysql:mysql-connector-java:5.1.6'
}

// Cucumber Configuration

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = 'cucumber.api.cli.Main'
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'model', 'src/test/resources']
        }
    }
}

// Checkstyle configuration

checkstyle {
    toolVersion = "6.1.1"
    configFile = "./config/checkstyle/google_checks.xml" as File
}

task createDatabase(type:Exec) {

  workingDir './'
  commandLine 'mysql', '-uroot'
  standardInput file('src/db/create-database.sql').newInputStream()
}

// Adding Cucumber:
// - Add dependencies to build.gradle
// - add cucumber task as above
// - Arguments for the task config are - https://cucumber.io/docs/reference/jvm#java
// - glue is the folder where the step defs are
// - last param is where the feature files
